plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
    id "com.google.gms.google-services"
    id 'com.google.firebase.crashlytics'
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

def keystoreProperties = new Properties()
def keystoreProductionProperties = new Properties()

def keystorePropertiesFile = rootProject.file('upload-key.properties')
def keystoreProductionPropertiesFile = rootProject.file('production-key.properties')

if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

if (keystoreProductionPropertiesFile.exists()) {
    keystoreProductionProperties.load(new FileInputStream(keystoreProductionPropertiesFile))
}

println("keystoreProperties: " + keystoreProperties)
println("keystoreProductionProperties: " + keystoreProductionProperties)

android {
    namespace "com.miittisoftwareoy.miitti_app"
    compileSdkVersion flutter.compileSdkVersion
    ndkVersion flutter.ndkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "com.miittisoftwareoy.miitti_app"
        // You can update the following values to match your application needs.
        // For more information, see: https://docs.flutter.dev/deployment/android#reviewing-the-gradle-build-configuration.
        minSdkVersion 23
        targetSdkVersion flutter.targetSdkVersion
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        multiDexEnabled true

    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }

        production {
            keyAlias keystoreProductionProperties['keyAlias']
            keyPassword keystoreProductionProperties['keyPassword']
            storeFile keystoreProductionProperties['storeFile'] ? file(keystoreProductionProperties['storeFile']) : null
            storePassword keystoreProductionProperties['storePassword']
        }
    }

    flavorDimensions 'default'

    productFlavors {
        production {
            dimension 'default'
            resValue "string", "app_name", "Miitti"
        }

        development {
            dimension 'default'
            applicationIdSuffix '.dev'
            versionNameSuffix '.dev'
            resValue "string", "app_name", "Miitti DEV"
        }

        staging {
            dimension 'default'
            applicationIdSuffix '.stg'
            versionNameSuffix '.stg'
            resValue "string", "app_name", "Miitti TEST"
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release // Ensure productionRelease uses the release keystore
        }

        debug {
            // Use the production keystore only for the production flavor debug builds
            matchingFallbacks = ['debug', 'release']
            signingConfig signingConfigs.production // Ensure productionDebug uses the production keystore
            minifyEnabled true // Disable shrinking for debug builds
        }
    }

    applicationVariants.all { variant ->
        if (variant.buildType.name == "debug" && variant.flavorName == "production") {
            variant.outputs.all { output ->
                println "Using production keystore for ${variant.name}"
                println "Keystore: ${variant.signingConfig?.storeFile}"
            }
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.9.10"
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation "androidx.browser:browser:1.5.0"

}
